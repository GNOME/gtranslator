dnl -------------------------------------------------------------------
dnl Process this file with autoconf to produce a configure script.
dnl -------------------------------------------------------------------
dnl (C) 2000-2001 Fatih Demir <kabalak@gtranslator.org>
dnl -------------------------------------------------------------------
AC_INIT(src/parse.c)
AM_CONFIG_HEADER(config.h)

dnl -------------------------------------------------------------------
dnl Init automake and the maintainer mode macros.
dnl -------------------------------------------------------------------
AM_INIT_AUTOMAKE(gtranslator, 0.39)
AM_MAINTAINER_MODE

dnl -------------------------------------------------------------------
dnl Use xml-i18n-tools.
dnl -------------------------------------------------------------------
AM_PROG_XML_I18N_TOOLS

dnl -------------------------------------------------------------------
dnl Pick up the Gnome macros .
dnl -------------------------------------------------------------------
AM_ACLOCAL_INCLUDE(macros)

dnl -------------------------------------------------------------------
dnl The standard checks for the compiler and the X includes/libs.
dnl -------------------------------------------------------------------

AC_ISC_POSIX
AC_PROG_CC
AC_HEADER_STDC
GNOME_INIT
GNOME_X_CHECKS

dnl -------------------------------------------------------------------
dnl The standard extended GNOME compile warning options ...
dnl -------------------------------------------------------------------
GNOME_COMPILE_WARNINGS

dnl -------------------------------------------------------------------
dnl Our languages caravane has got settled here in place.( add your's )
dnl -------------------------------------------------------------------
ALL_LINGUAS="az da de el en_GB es fi fr gl hu it ja ko lt nl no pl pt_BR ru sv tr uk wa"
AM_GNOME_GETTEXT

dnl -----------------------------------------------------------
dnl Test the version of GnomeVFS to be >= 0.4.1
dnl -----------------------------------------------------------
AC_MSG_CHECKING(for GnomeVFS >= 0.4.1)
if gnome-config --libs vfs 2>&1 >/dev/null ; then
	VFS_VERSION=`gnome-config --modversion vfs | sed -e 's/^[[^0-9]]*//' | \
	awk -F. '{ print $1 * 10000 + $2 * 100 + $3;}'`
	
	if test "$VFS_VERSION" -ge 401 ; then
		AC_MSG_RESULT(found)			
	else
		AC_MSG_ERROR([
"------------------------------------------------------------------------------"
"Please install GnomeVFS >= 0.4.1 and try again."
"-----------------------------------------------------------------------------"])
	fi
else
	AC_MSG_ERROR([
"-----------------------------------------------------------------------------"	
"Please install GnomeVFS before trying to compile gtranslator."
"-----------------------------------------------------------------------------"])
fi

dnl -------------------------------------------------------------------
dnl Define the compile flags for gtranslator
dnl -------------------------------------------------------------------
VFS_CFLAGS=`gnome-config --cflags vfs`
VFS_LIBS=`gnome-config --libs vfs`
AC_SUBST(VFS_CFLAGS)
AC_SUBST(VFS_LIBS)

dnl -------------------------------------------------------------------
dnl Check for gal >= 0.5.
dnl -------------------------------------------------------------------
AC_MSG_CHECKING(for gal >= 0.5)
if gnome-config --libs gal 2>&1 >/dev/null ; then
	dnl -----------------------------------------------------------
	dnl gal is present; check for the right version numbers.
	dnl -----------------------------------------------------------
	GAL_VERSION=`gnome-config --modversion gal | sed -e 's/^[[^0-9]]*//' | \
	awk -F. '{ print $1 * 10000 + $2 * 100 + $3;}'`
	
	if test "$GAL_VERSION" -ge 500 ; then
		AC_MSG_RESULT(found)			
	else
		dnl ---------------------------------------------------
		dnl Tell the user that we've found gal, but it's too
		dnl  old for us.
		dnl ---------------------------------------------------
		AC_MSG_ERROR([
"-----------------------------------------------------------------------------"
"Please install gal >= 0.5 and try configuring again."
"-----------------------------------------------------------------------------"])
	fi
else
	AC_MSG_ERROR([
"-----------------------------------------------------------------------------"
"Please install gal before configuring again."
"-----------------------------------------------------------------------------"])
fi

GAL_CFLAGS=`gnome-config --cflags gal`
GAL_LIBS=`gnome-config --libs gal`
AC_SUBST(GAL_CFLAGS)
AC_SUBST(GAL_LIBS)

dnl -------------------------------------------------------------------
dnl Check for GConf >= 0.9 and the stuff around it ..
dnl -------------------------------------------------------------------

GCONF_CFLAGS=""
GCONF_LIBS=""

try_gconf=true
AC_ARG_WITH(gconf,
	[  --{with,without}-gconf    Compile with GConf support or without it],
	if test x$withval = xno; then
		try_gconf=false
	fi
)

gconf_is_present=yes
if $try_gconf; then
	AM_PATH_GCONF(0.9.0,,gconf_is_present=no,gconf-gtk)
	if test "x$gconf_is_present" = "xno" ; then
		AC_MSG_WARN([
"-----------------------------------------------------------------------------"
"No recent enough GConf found."
"-----------------------------------------------------------------------------"])
	else
		GCONF_CFLAGS="$GCONF_CFLAGS -DGCONF_IS_PRESENT"
	fi
else
	echo "-----------------------------------------------------------------------------"
	echo "Not checking for GConf"
	echo "-----------------------------------------------------------------------------"
	gconf_is_present=no
fi

AC_SUBST(GCONF_CFLAGS)
AC_SUBST(GCONF_LIBS)

dnl -------------------------------------------------------------------
dnl Check for the window-icon feature of gnome-libs > 1.0.59
dnl -------------------------------------------------------------------
MY_CFLAGS=$CFLAGS
MY_LIBS=$LIBS
CFLAGS="$CFLAGS `gnome-config --cflags gnomeui`"
LIBS="$LIBS `gnome-config --libs gnomeui`"
use_window_icon=no
AC_CHECK_LIB(gnomeui, gnome_window_icon_set_default_from_file, use_window_icon=yes)
CFLAGS=$MY_CFLAGS
LIBS=$MY_LIBS
AM_CONDITIONAL(USE_WINDOW_ICON, test z$use_window_icon = zyes)

dnl -------------------------------------------------------------------
dnl Simply substitute the icon-location directory
dnl in gtranslator.keys.in .
dnl -------------------------------------------------------------------
PIXMAP_DIR="`gnome-config --datadir`/pixmaps/mc"
AC_SUBST(PIXMAP_DIR)

dnl -------------------------------------------------------------------
dnl Whether to bind the po-files with gtranslator/bind the mime-type ?
dnl -------------------------------------------------------------------
AC_ARG_ENABLE(mime-bind,
	[  --enable-mime-bind=[yes/no]   Bind the po-files mime-type to gtranslator],, enable_mime_bind=yes)
AC_MSG_CHECKING([if to bind the po-files with gtranslator ])
if test "x$enable_mime_bind" = "xyes" ; then
	AC_DEFINE(BIND_MIME)
fi
AC_MSG_RESULT($enable_mime_bind)
AM_CONDITIONAL(BIND_MIME,test x$enable_mime_bind = xyes)

dnl -------------------------------------------------------------------
dnl Check for nautilus and try to get out all the flags for the
dnl  nautilus-view.
dnl -------------------------------------------------------------------
build_nv=no
PRE_LIBS="$LIBS"
NAUTILUS_CFLAGS=
NAUTILUS_LIBS=

AC_MSG_CHECKING([whether nautilus-gtranslator-view should be build])
AC_ARG_ENABLE(nautilus-view,
[  --enable-nautilus-view        Enable nautilus-gtranslator-view], build_nv=yes,)

dnl -------------------------------------------------------------------
dnl Check if the --enable switch had been given.
dnl -------------------------------------------------------------------

if test "x${build_nv}" = "xno" ; then
	AC_MSG_RESULT(no)
else
	nautilus_prefix=`which nautilus|sed -e s%/bin/nautilus%%g`

	if test "z${nautilus_prefix}" = "z"; then
		AC_MSG_RESULT([
"-----------------------------------------------------------------------------"
"No Nautilus prefix could be determined -- not building nautilus-gtranslator-view"
"-----------------------------------------------------------------------------"])
		build_nv=no
	else
		build_nv=no
		mycheck_function="nautilus_view_standard_main_multi"
		AC_MSG_RESULT([
"-----------------------------------------------------------------------------"
"${nautilus_prefix} as Nautilus prefix determined: checking for Nautilus libraries"
"-----------------------------------------------------------------------------"])
		LIBS="`gnome-config --libs gnome gnomeui bonobo bonobox gal vfs` \
		-L${nautilus_prefix}/lib -lnautilus"
		AC_CHECK_LIB(nautilus, ${mycheck_function},
			build_nv=yes,
			AC_MSG_WARN([
"-----------------------------------------------------------------------------"
"libnautilus does not contain ${mycheck_function}: Not building Nautilus view"
"-----------------------------------------------------------------------------"]))
	fi

	dnl -------------------------------------------------------------------
	dnl Get the nautilus compile flags for the view..
	dnl -------------------------------------------------------------------
	if test "x$build_nv" = "xyes" ; then
		AC_MSG_CHECKING([for necessary Nautilus headers in ${nautilus_prefix}/include/libnautilus])	
		if test -f "${nautilus_prefix}/include/libnautilus/nautilus-view.h" -a \
			-f "${nautilus_prefix}/include/libnautilus/nautilus-view-standard-main.h" -a \
			-f "${nautilus_prefix}/include/libnautilus/nautilus-bonobo-ui.h" ; then
			AC_MSG_RESULT(found)
			NAUTILUS_CFLAGS="`gnome-config --cflags oaf vfs bonobo bonobox` \
			-I${nautilus_prefix}/include/libnautilus \
			`gconf-config --cflags gconf-gtk`"
			NAUTILUS_LIBS="`gnome-config --libs oaf vfs bonobo bonobox` \
			-lnautilus `gconf-config --libs gconf-gtk`"
		else
			AC_MSG_WARN([
"-----------------------------------------------------------------------------"
"Could not find the necessary headers of nautilus: please install also the headers"
"dev/devel packages of nautilus and reconfigure gtranslator if you also want to"
"see the nautilus-gtranslator-view in action."
"-----------------------------------------------------------------------------"])
			build_nv=no
		fi
	fi
fi

dnl -------------------------------------------------------------------
dnl Rescue the previous $LIBS and define the NAUTILUS_CFLAGS for 
dnl  compiling and NAUTILUS_LIBS for linking with -lnautilus.
dnl -------------------------------------------------------------------
LIBS="$PRE_LIBS"
AC_SUBST(NAUTILUS_CFLAGS)
AC_SUBST(NAUTILUS_LIBS)
AM_CONDITIONAL(BUILD_NV, test x$build_nv = xyes)

dnl -------------------------------------------------------------------
dnl Check for Scrollkeeper and set the corresponding conditional var if
dnl  it could be found.
dnl -------------------------------------------------------------------
AC_CHECK_PROG(sc_present, scrollkeeper-config, yes, no)
if test "z$sc_present" = "zyes" ; then
	AC_MSG_CHECKING([for scrollkeeper >= 0.1.4])
	_VERSION=`scrollkeeper-config --version|sed -e 's/\.//g'`
	if test $_VERSION -ge 014 -o $_VERSION -ge 02; then
		AC_MSG_RESULT(yes)
		SC_STATEDIR=`scrollkeeper-config --pkglocalstatedir`
		SC_OMFDIR=`scrollkeeper-config --omfdir`
	else
		AC_MSG_RESULT(no)
		sc_present=no
	fi
fi
AC_SUBST(SC_OMFDIR)
AC_SUBST(SC_STATEDIR)
AM_CONDITIONAL(SCROLLKEEPER_PRESENT, test z$sc_present = zyes)

dnl -------------------------------------------------------------------
dnl Enable debug builds.
dnl -------------------------------------------------------------------
AC_ARG_ENABLE(debug,
	[  --enable-debug                Turn on debugging flags [default=no]],
	,enable_debug=no)
AC_MSG_CHECKING([if a debug friendly version should be build])
if test "x$enable_debug" = "xyes" ; then
	CFLAGS="$CFLAGS -ggdb -Werror"
else
	CFLAGS="`echo $CFLAGS|sed -e s/-g.//g -e s/-ggdb//g`"
fi
AC_MSG_RESULT($enable_debug)

dnl -------------------------------------------------------------------
dnl Get the date for the man-page and substitute it there and anywhere.
dnl -------------------------------------------------------------------
MY_DATE=`date +%Y-%m-%d`
AC_SUBST(MY_DATE)

dnl -------------------------------------------------------------------
dnl Put out the configured files.
dnl -------------------------------------------------------------------
AC_OUTPUT([
Makefile
gtranslator.spec
data/Makefile
data/colorschemes/Makefile
data/desktop/Makefile
data/gconf/Makefile
data/mime/Makefile
data/mime/gtranslator.keys
data/oaf/Makefile
data/scripts/Makefile
data/ui/Makefile
help/Makefile
help/C/Makefile
help/ja/Makefile
intl/Makefile
macros/Makefile
man/Makefile
man/gtranslator.1
man/nautilus-gtranslator-view.1
nautilus-view/Makefile
po/Makefile.in
src/Makefile
src/pixmaps/Makefile
])

dnl ------------------------------------------------------------------
dnl Print an information screen at the end of the process
dnl ------------------------------------------------------------------
echo "
------------------------------------------------------------------
-- gtranslator compilation options

gtranslator version	  :	${VERSION}

bind the po & co. files	  :	${enable_mime_bind:-possibly}	

use window-icon		  :	${use_window_icon:-no}

GAL & GnomeVFS version	  :	`gnome-config --modversion gal|sed -e 's/.*-//g'` & `gnome-config --modversion vfs|sed -e 's/.*-//g'`

CFLAGS                 	  :	${CFLAGS}

GConf based config	  :	${gconf_is_present:-no}

debug friendly build	  :	${enable_debug:-no}

nautilus-gtranslator-view :	${build_nv:-no}

Scrollkeeper integration  :	${sc_present:-no}
------------------------------------------------------------------
"
