dnl -------------------------------------------------------------------
dnl Process this file with autoconf to produce a configure script .
dnl -------------------------------------------------------------------

AC_INIT(src/parse.c)
AM_CONFIG_HEADER(config.h)

dnl -------------------------------------------------------------------
dnl Versioning stuff
dnl -------------------------------------------------------------------
MAINVERSION=0
SUBVERSION=40
VERSION=$MAINVERSION.$SUBVERSION
AC_SUBST(SUBVERSION)
AC_SUBST(MAINVERSION)

dnl -------------------------------------------------------------------
dnl Init automake and the maintainer mode macros.
dnl -------------------------------------------------------------------
AM_INIT_AUTOMAKE(gtranslator, $VERSION)
AM_MAINTAINER_MODE

dnl -------------------------------------------------------------------
dnl Use xml-i18n-tools.
dnl -------------------------------------------------------------------
AM_PROG_XML_I18N_TOOLS

dnl -------------------------------------------------------------------
dnl Pick up the Gnome macros .
dnl -------------------------------------------------------------------
AM_ACLOCAL_INCLUDE(macros)

dnl -------------------------------------------------------------------
dnl The standard checks for the compiler and the X includes/libs.
dnl -------------------------------------------------------------------

GNOME_INIT
AC_ISC_POSIX
AC_PROG_CC
AC_HEADER_STDC
GNOME_X_CHECKS

dnl -------------------------------------------------------------------
dnl Libtool for the libgtranslator directory and the directory.
dnl -------------------------------------------------------------------
AM_PROG_LIBTOOL

dnl -------------------------------------------------------------------
dnl The standard extended GNOME compile warning options ...
dnl -------------------------------------------------------------------
GNOME_COMPILE_WARNINGS

dnl -------------------------------------------------------------------
dnl Check for glib and gtk+ indepententally from the GNOME checks 
dnl -------------------------------------------------------------------
AM_PATH_GLIB(1.2.3)

dnl -------------------------------------------------------------------
dnl We need Gtk+ >= 1.2.4 for D'n'D, do we? I don't know it, but I've 
dnl  got heard somewhere that proper D'n'D was integrated with 1.2.4.
dnl -------------------------------------------------------------------
AM_PATH_GTK(1.2.4)

dnl -------------------------------------------------------------------
dnl Our languages caravane has got settled here in place.( add your's )
dnl -------------------------------------------------------------------
ALL_LINGUAS="da de en_GB es fi fr gl hu it ja ko lt nl no pl pt_BR ru sv tr uk"
AM_GNOME_GETTEXT

dnl -------------------------------------------------------------------
dnl Check for the window-icon feature of gnome-libs > 1.0.59.
dnl -------------------------------------------------------------------
MY_CFLAGS=$CFLAGS
MY_LIBS=$LIBS
CFLAGS="$CFLAGS `gnome-config --cflags gnomeui`"
LIBS="$LIBS `gnome-config --libs gnomeui`"
AC_CHECK_LIB(gnomeui, gnome_window_icon_set_default_from_file, 
	AC_MSG_RESULT(["+++ Will use window-icon feature..."])
	AC_DEFINE(USE_WINDOW_ICON)
	,[
	w
	AC_MSG_RESULT(["+++ Cannot use the window-icon feature as you have not got a recent enough gnome-libs version"])])
CFLAGS=$MY_CFLAGS
LIBS=$MY_LIBS

dnl -------------------------------------------------------------------
dnl Get the idl parameters.
dnl -------------------------------------------------------------------
IDL_CFLAGS=`gnome-config --cflags idl`
IDL_LIBS=`gnome-config --libs idl`
AC_SUBST(IDL_CFLAGS)
AC_SUBST(IDL_LIBS)

dnl -------------------------------------------------------------------
dnl Check for ORBit >= 0.5.3, OAF 0.6
dnl -------------------------------------------------------------------
AM_PATH_ORBIT(0.5.3)
AM_PATH_OAF(0.6.0)

dnl -------------------------------------------------------------------
dnl Check for GConf >= 0.9 and the stuff around it ..
dnl -------------------------------------------------------------------
gconf_is_present=yes
AM_PATH_GCONF(0.9.0,,gconf_is_present=no,gconf-gtk)
if test "q$gconf_is_present" = "qno" ; then
	AC_MSG_RESULT(["+++ Will NOT use GConf..."])
else
	if test "ENV$NO_GCONF" = "ENVyes" ; then
	gconf_is_present=no
	AC_MSG_RESULT(["+++ Will not use the found GConf as \$NO_GCONF is set to _yes_ ..."])
	else
	AC_MSG_RESULT(["+++ Will use GConf..."])
	fi
fi
GNOME_CFLAGS=`gnome-config --cflags gnome`
GNOME_LIBS=`gnome-config --libs gnome`
GNOME_LIB_CONF_DIR=`gnome-config --libdir`
AC_SUBST(GNOME_CFLAGS)
AC_SUBST(GNOME_LIBS)
AC_SUBST(GNOME_LIB_CONF_DIR)
AM_CONDITIONAL(GCONF_IS_PRESENT, test x$gconf_is_present = xyes)

dnl -------------------------------------------------------------------
dnl Check for libxml >= 1.8.9 and get the cflags and libs.
dnl -------------------------------------------------------------------
AC_PATH_PROG(XML_CONFIG, xml-config, nej)
if test "our$XML_CONFIG" = "ournej" ; then
	AC_MSG_ERROR([No xml-config found ! Please install libxml first.])
else
	AC_MSG_CHECKING([for libxml >= 1.8.9])
	XML_VERSION=`xml-config --version|sed -e 's/\.//g'`
	if test "$XML_VERSION" -lt 189 ; then
		AC_MSG_RESULT("no")
		AC_MSG_ERROR("Your libxml is too old!")
	else
		AC_MSG_RESULT(["yes, version `xml-config --version`"])
	fi
	XML_CFLAGS=`xml-config --cflags`
	XML_LIBS=`xml-config --libs`
	AC_SUBST(XML_CFLAGS)
	AC_SUBST(XML_LIBS)
fi

dnl -------------------------------------------------------------------
dnl Check for GnomeVFS via this crappy check and get the libs and 
dnl  compile flags for it or disable USE_VFS_STUFF.
dnl -------------------------------------------------------------------
vfs_stuff=no
AC_MSG_CHECKING(for GnomeVFS >= 0.4.1)
if gnome-config --cflags vfs 2>&1 >/dev/null ; then
	dnl -----------------------------------------------------------
	dnl Temporarily we're now happy :-)
	dnl -----------------------------------------------------------
	vfs_stuff=yes
	dnl -----------------------------------------------------------
	dnl Test the version of GnomeVFS to be >= 0.4.1
	dnl -----------------------------------------------------------
	VFS_VERSION=`gnome-config --modversion vfs|sed -e 's/.*-//g' -e 's/\.//g'`
	if test "$VFS_VERSION" -ge 41 ; then
		AC_MSG_RESULT([" "
"+++ Right your GnomeVFS is recent enough for us... "])	
	else
		vfs_stuff=no
		AC_MSG_RESULT([" "
"+++ Your GnomeVFS is too old, it is $VFS_VERSION < gnome-vfs 0.4"])
	fi
	dnl -----------------------------------------------------------
	dnl Get the CFLAGS and the LIBS
	dnl -----------------------------------------------------------
	VFS_CFLAGS=`gnome-config --cflags vfs`
	VFS_LIBS=`gnome-config --libs vfs`
	dnl -----------------------------------------------------------
	dnl Define the USE_VFS_STUFF directive...
	dnl -----------------------------------------------------------
	if test "x$vfs_stuff" = "xyes" ; then
	AC_MSG_RESULT(["+++ Defining VFS flags... "])
		AC_SUBST(VFS_CFLAGS)
		AC_SUBST(VFS_LIBS)
	fi	
else
	dnl -----------------------------------------------------------
	dnl Oh, no GnomeVFS ;-(
	dnl -----------------------------------------------------------
	AC_MSG_RESULT([" "
"+++ GnomeVFS was not found... Not building, linking with VFS stuff."])	
fi
AM_CONDITIONAL(USE_VFS_STUFF, test x$vfs_stuff = xyes)

dnl -------------------------------------------------------------------
dnl Check for gal >= 0.4.99.
dnl -------------------------------------------------------------------
gal_gui=no
GAL_CFLAGS=
GAL_LIBS=
AC_MSG_CHECKING(for gal >= 0.4.99)
if gnome-config --modversion gal 2>&1 >/dev/null ; then
	dnl -----------------------------------------------------------
	dnl gal is present; check for the right version numbers.
	dnl -----------------------------------------------------------
	_GAL_VERSION=`gnome-config --modversion gal|sed -e 's/^.*-0\.//g' -e 's/\.//g'`
	if test $_GAL_VERSION -ge 499 ; then
		dnl ---------------------------------------------------
		dnl We've found gal >= 0.4.99, good for us...
		dnl ---------------------------------------------------
		AC_MSG_RESULT([found])
		AC_MSG_RESULT(["+++ Using gal-enhanced GUI..."])
		GAL_CFLAGS=`gnome-config --cflags gal`
		GAL_LIBS=`gnome-config --libs gal`
		gal_gui=yes
	else
		dnl ---------------------------------------------------
		dnl Tell the user that we've found gal, but it's too
		dnl  old for us.
		dnl ---------------------------------------------------
		AC_MSG_RESULT([found a too old version])
		AC_MSG_RESULT(["+++ Using standard GUI..."])
	fi
else
	dnl -----------------------------------------------------------
	dnl No gal found at all? No problem, simply use the current
	dnl  standard GUI.
	dnl -----------------------------------------------------------
	AC_MSG_RESULT([no gal found])
	AC_MSG_RESULT(["+++ Using standard GUI..."])
fi
AC_SUBST(GAL_CFLAGS)
AC_SUBST(GAL_LIBS)
AM_CONDITIONAL(USE_GAL_GUI, test z$gal_gui = zyes)

dnl -------------------------------------------------------------------
dnl Check for gtkhtml > 0.8
dnl -------------------------------------------------------------------
gtkhtml_enhancement=no
GTKHTML_CFLAGS=
GTKHTML_LIBS=
AC_MSG_CHECKING(for gtkhtml >= 0.8)
if gnome-config --modversion gtkhtml 2>&1 >/dev/null ; then
	dnl -----------------------------------------------------------
	dnl Check for the right version of it.
	dnl -----------------------------------------------------------
	_GTKHTML_VERSION=`gnome-config --modversion gtkhtml|sed -e 's/^.*-0\.//g' -e 's/\.//g'`
	dnl -----------------------------------------------------------
	dnl Test for a really recent version.
	dnl -----------------------------------------------------------
	if test $_GTKHTML_VERSION -ge 80 ; then
		AC_MSG_RESULT([found])
		AC_MSG_RESULT(["+++ Using GtkHTML based editing GUI..."])
		GTKHTML_CFLAGS=`gnome-config --cflags gtkhtml`
		GTKHTML_LIBS=`gnome-config --libs gtkhtml`
		gtkhtml_enhancement=yes
	else
		AC_MSG_RESULT([too old for us])
		AC_MSG_RESULT(["+++ Using standard text based editing GUI..."])
	fi
else
	dnl -----------------------------------------------------------
	dnl Do use the standard text GUI.
	dnl -----------------------------------------------------------
	AC_MSG_RESULT([no gtkhtml found])
	AC_MSG_RESULT(["+++ Using standard text based editing GUI..."])
fi
AC_SUBST(GTKHTML_CFLAGS)
AC_SUBST(GTKHTML_LIBS)
AM_CONDITIONAL(USE_GTKHTML, test y$gtkhtml_enhancement = yyes)

dnl -------------------------------------------------------------------
dnl Substitute PACKAGE_LOCALE_DIR in config.h.
dnl -------------------------------------------------------------------
if test "x${prefix}" = "xNONE"; then
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${ac_default_prefix}/${DATADIRNAME}/locale")
else
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${prefix}/${DATADIRNAME}/locale")
fi

dnl -------------------------------------------------------------------
dnl Simply substitute the icon-location directory
dnl in gtranslator.keys.in .
dnl -------------------------------------------------------------------
PIXMAP_DIR="`gnome-config --datadir`/pixmaps/mc"
AC_SUBST(PIXMAP_DIR)

dnl -------------------------------------------------------------------
dnl Whether to bind the po-files with gtranslator/bind the mime-type ?
dnl -------------------------------------------------------------------
AC_ARG_ENABLE(mime-bind,
	[  --enable-mime-bind=[yes/no]      Bind the po-files mime-type to gtranslator],,enable_mime_bind=yes)
AC_MSG_CHECKING([if to bind the po-files with gtranslator ])
if test "x$enable_mime_bind" = "xyes" ; then
	AC_DEFINE(BIND_MIME)
	AC_MSG_RESULT([" "
"+++ Will install the mime and keys files."])
else
	AC_MSG_RESULT([" "
"+++ Not installing the mime and keys files for the mime-types."])
fi
AM_CONDITIONAL(BIND_MIME,test x$enable_mime_bind = xyes)

dnl -------------------------------------------------------------------
dnl Enable debug builds.
dnl -------------------------------------------------------------------
AC_ARG_WITH(debug-specs,
     [  --with-debug-specs		   Build everything with debug informations],, with_debug_specs=yes)
AC_MSG_CHECKING([for a debuggy build ])
if test "rt$with_debug_specs" = "rtno" ; then
	AC_MSG_RESULT([" "
"+++ No, building everything without any debugging informations and flags..."])
	CFLAGS="`echo $CFLAGS|sed -e 's/-g.//g'`"
else
	AC_MSG_RESULT([" "
"+++ Yes, building everything WITH debugging informations and flags..."])
	CFLAGS="`echo $CFLAGS|sed -e 's/-O.//g'` -ggdb"
fi

dnl -------------------------------------------------------------------
dnl For the extremists of the 'gcc-&-Werror-are-friends' department.
dnl -------------------------------------------------------------------
AC_ARG_WITH(extreme-gcc-flags,
     [  --with-extreme-gcc-flags	   Set the CFLAGS to the most extreme values],, extreme_gcc_flags=yes)
AC_MSG_CHECKING([for extremists-ala-gcc-Werror ])
if test "gcc$extreme_gcc_flags" = "gccyes" ; then
	AC_MSG_RESULT([" "
"+++ Yes, you are getting your -Werror -Wformat etc. ..."])
	CFLAGS="$CFLAGS -Werror -Wtrigraphs -Wformat -Wchar-subscripts"
	CFLAGS="$CFLAGS	-Wimplicit -Wreturn-type -Wswitch -Wcomment"
else
	AC_MSG_RESULT([" "
"+++ Huh, no gcc extremist =?"])
fi

dnl -------------------------------------------------------------------
dnl Try to get a pkg-config directory for us ..
dnl -------------------------------------------------------------------
AC_MSG_CHECKING([where the pkg-config file should go in])
PKG_CONF_CONFFILE_DIR="$PKG_CONFIG_PATH"
if test "foo${PKG_CONF_CONFFILE_DIR}" = "foo" ; then
	dnl ----------------------------------------------------------
	dnl Try the standard locations if no variable $PKG_CONFIG_PATH
	dnl  has been defined on shell-level.
	dnl ----------------------------------------------------------
	for dirX in /usr/lib/pkgconfig /usr/libexec/pkgconfig /usr/local/lib/pkgconfig /usr/local/libexec/pkgconfig
		do
			if test -d "$dirX" ; then
				PKG_CONF_CONFFILE_DIR="$dirX"
				AC_MSG_RESULT([" "
"+++ Taking ${PKG_CONF_CONFFILE_DIR} as pkg-config supplement files directory"])
			fi
		done
	dnl ----------------------------------------------------------
	dnl Test if we did find any pkgconfig directory and use the 
	dnl  default pkgconfig dir "/usr/lib/pkgconfig" in this case..
	dnl ----------------------------------------------------------
	if test "foo${PKG_CONF_CONFFILE_DIR}" = "foo" ; then
		AC_MSG_RESULT([" "
"+++ No existing pkgconfig directory found, using /usr/lib/pkgconfig ..."])
		PKG_CONF_CONFFILE_DIR="/usr/lib/pkgconfig"
	fi
else
	AC_MSG_RESULT([" "
"+++ Taking ${PKG_CONF_CONFFILE_DIR} as pkg-config supplement files directory"])
fi
AC_SUBST(PKG_CONF_CONFFILE_DIR)

dnl -------------------------------------------------------------------
dnl Get the date for the man-page and substitute it there and anywhere.
dnl -------------------------------------------------------------------
MY_DATE=`date +%Y-%m-%d`
AC_SUBST(MY_DATE)

dnl -------------------------------------------------------------------
dnl Put out the configured files.
dnl -------------------------------------------------------------------
AC_OUTPUT([
Makefile
data/Makefile
data/desktop/Makefile
data/gconf/Makefile
data/gnome-config/gtranslatorConf.sh
data/gnome-config/Makefile
data/mime/Makefile
data/mime/gtranslator.keys.in
data/oaf/Makefile
data/pkg-config/Makefile
data/pkg-config/gtranslator.pc
data/scripts/Makefile
macros/Makefile
intl/Makefile
src/Makefile
src/pixmaps/Makefile
po/Makefile.in
help/Makefile
help/C/Makefile
help/C/index.html
help/tr/Makefile
help/tr/index.html
man/Makefile
man/gtranslator.1
libgtranslator/Makefile
])

dnl ------------------------------------------------------------------
dnl Print an information screen at the end of the process
dnl ------------------------------------------------------------------
if test "q${with_message_db_dir}" = "qNONE" ; then
	with_message_db_dir=
fi
grep -sq USE_WINDOW_ICON\ 1  config.h && \
use_window_icon_feature=yes
echo "
------------------------------------------------------------------
-- gtranslator compilage options

gtranslator version	:	${VERSION}

bind the po-files  
 with gtranslator  	:	${enable_mime_bind:-possibly}	

use the window-icon
 feature of gnome-libs  :	${use_window_icon_feature:-no}

use the GAL
 for the interface	:	${gal_gui:-no}

use GtkHTML
 as editing interface	:	${gtkhtml_enhancement:-no}

if we can use GConf
 for the config-stuff	:	${gconf_is_present:-no}

if libgtranslator can
 use GnomeVFS stuff	:	${vfs_stuff:-no}

if everything should be 
 compiled debuggy	:	${with_debug_specs:-no}

if you're getting your
 loved -Werror etc.	:	${extreme_gcc_flags:-no}

------------------------------------------------------------------
"
