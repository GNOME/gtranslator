dnl -------------------------------------------------------------------
dnl Process this file with autoconf to produce a configure script.
dnl -------------------------------------------------------------------
dnl (C) 2000-2002 Fatih Demir <kabalak@gtranslator.org>
dnl -------------------------------------------------------------------
AC_INIT(src/main.c)
AM_INIT_AUTOMAKE(gtranslator, 0.51)
AM_CONFIG_HEADER(config.h)
AM_MAINTAINER_MODE

dnl -------------------------------------------------------------------
dnl Use intltools.
dnl -------------------------------------------------------------------
AC_PROG_INTLTOOL(0.11)

dnl -------------------------------------------------------------------
dnl Libtool -- needed for backends.
dnl -------------------------------------------------------------------
AC_ISC_POSIX
AC_DISABLE_STATIC
AM_PROG_LIBTOOL

dnl -------------------------------------------------------------------
dnl Pick up the Gnome macros .
dnl -------------------------------------------------------------------
AM_ACLOCAL_INCLUDE(macros)

dnl -------------------------------------------------------------------
dnl The standard checks for the compiler and the X includes/libs.
dnl -------------------------------------------------------------------

AC_HEADER_STDC
dnl GNOME_INIT
dnl GNOME_X_CHECKS

dnl -------------------------------------------------------------------
dnl The standard extended GNOME compile warning options ...
dnl -------------------------------------------------------------------
GNOME_COMPILE_WARNINGS(maximum)

dnl -------------------------------------------------------------------
dnl Our languages caravane has got settled here in place (add your's).
dnl -------------------------------------------------------------------
ALL_LINGUAS="az ca da de el en_GB es et eu fi fr gl hu it ja ko lt nl nn no pl pt pt_BR ru sk sv ta tr uk wa zh_CN zh_TW"
AM_GNOME_GETTEXT

dnl ==========================================================================
dnl
dnl If you add a version number here, you *must* add an AC_SUBST line for
dnl it too, or it will never make it into the spec file!
dnl
dnl ==========================================================================

GTK_REQUIRED=1.3.9
LIBGNOME_REQUIRED=1.96.0
LIBGNOMEUI_REQUIRED=1.96.0
LIBBONOBO_REQUIRED=1.107.0
LIBBONOBOUI_REQUIRED=1.108.1
LIBGNOMECANVAS_REQUIRED=1.108.0
GNOME_BUILD_REQUIRED=0.0.3
GDL_REQUIRED=0.3.0
GNOME_VFS_REQUIRED=1.9.4
GNOME_VFS_REQUIRED=1.9.4
LIBXML_REQUIRED=2.4.12

AC_SUBST(GTK_REQUIRED)
AC_SUBST(LIBGNOME_REQUIRED)
AC_SUBST(LIBGNOMEUI_REQUIRED)
AC_SUBST(LIBBONOBO_REQUIRED)
AC_SUBST(LIBBONOBOUI_REQUIRED)
AC_SUBST(LIBGNOMECANVAS_REQUIRED)
AC_SUBST(GNOME_BUILD_REQUIRED)
AC_SUBST(GNOME_VFS_REQUIRED)
AC_SUBST(LIBXML_REQUIRED)

PKG_CHECK_MODULES(GTRANSLATOR, libgnome-2.0 >= $LIBGNOME_REQUIRED libgnomeui-2.0 >= $LIBGNOMEUI_REQUIRED libbonobo-2.0 >= $LIBBONOBO_REQUIRED libbonoboui-2.0 >= $LIBBONOBOUI_REQUIRED gnome-vfs-2.0 >= $GNOME_VFS_REQUIRED libxml-2.0 >= $LIBXML_REQUIRED)

AC_SUBST(GTRANSLATOR_CFLAGS)
AC_SUBST(GTRANSLATOR_LIBS)

DEPRECATED_FLAGS="-DG_DISABLE_DEPRECATED -DGTK_DISABLE_DEPRECATED -DGNOME_DISABLE_DEPRECATED"
AC_SUBST(DEPRECATED_FLAGS)


dnl -------------------------------------------------------------------
dnl Whether we use the depricated gtk+ functions
dnl -------------------------------------------------------------------
AC_ARG_ENABLE(gtk_enable_broken, [  --enable-gtk-broken     use depricated gtk functions ],,gtk_enable_broken=yes)
AC_DEFINE(GTK_ENABLE_BROKEN)

dnl -------------------------------------------------------------------
dnl Whether to bind the po-files with gtranslator/bind the mime-type ?
dnl -------------------------------------------------------------------
AC_ARG_ENABLE(mime-bind,
	[  --enable-mime-bind=[yes/no]   Bind the po-files mime-type to gtranslator],, enable_mime_bind=yes)
AC_MSG_CHECKING([if to bind the po-files with gtranslator ])
if test "x$enable_mime_bind" = "xyes" ; then
	AC_DEFINE(BIND_MIME)
fi
AC_MSG_RESULT($enable_mime_bind)
AM_CONDITIONAL(BIND_MIME,test x$enable_mime_bind = xyes)

dnl -------------------------------------------------------------------
dnl Check for Scrollkeeper and set the corresponding conditional var if
dnl  it could be found.
dnl -------------------------------------------------------------------
AC_CHECK_PROG(sc_present, scrollkeeper-config, yes, no)
if test "z$sc_present" = "zyes" ; then
	AC_MSG_CHECKING([for scrollkeeper >= 0.1.4])
	_VERSION=`scrollkeeper-config --version|sed -e 's/\.//g'`
	if test $_VERSION -ge 014 -o $_VERSION -ge 02; then
		AC_MSG_RESULT(yes)
		SC_STATEDIR=`scrollkeeper-config --pkglocalstatedir`
		SC_OMFDIR=`scrollkeeper-config --omfdir`
	else
		AC_MSG_RESULT(no)
		sc_present=no
	fi
fi
AC_SUBST(SC_OMFDIR)
AC_SUBST(SC_STATEDIR)
AM_CONDITIONAL(SCROLLKEEPER_PRESENT, test z$sc_present = zyes)

dnl -------------------------------------------------------------------
dnl Check for jw to get the docs building fine with RH 7.1
dnl -------------------------------------------------------------------
AC_CHECK_PROG(jw_present, jw, yes, no)
AM_CONDITIONAL(JW_PRESENT, test e$jw_present = eyes)

dnl -------------------------------------------------------------------
dnl Enable debug builds.
dnl -------------------------------------------------------------------
AC_ARG_ENABLE(debug,
	[  --enable-debug                Turn on debugging flags [default=yes]],
	,enable_debug=yes)
AC_MSG_CHECKING([if a debug friendly version should be build])
if test "x$enable_debug" = "xyes" ; then
	CFLAGS="$CFLAGS -ggdb -Werror"
else
	CFLAGS="`echo $CFLAGS|sed -e s/-g.//g -e s/-ggdb//g`"
fi
AC_MSG_RESULT($enable_debug)

dnl -------------------------------------------------------------------
dnl Get the date for the man-page and substitute it there and anywhere.
dnl -------------------------------------------------------------------
MY_DATE=`date +%Y-%m-%d`
AC_SUBST(MY_DATE)

dnl -------------------------------------------------------------------
dnl The most important check for a library at all!
dnl -------------------------------------------------------------------
AC_CHECK_LIB(ife, do_you_think_we_are_just_geeks,,)

dnl -------------------------------------------------------------------
dnl Put out the configured files.
dnl -------------------------------------------------------------------
AC_OUTPUT([
Makefile
gtranslator.spec
data/Makefile
data/colorschemes/Makefile
data/desktop/Makefile
data/etspecs/Makefile
data/mime/Makefile
data/mime/gtranslator.keys
data/pixmaps/Makefile
data/scripts/Makefile
debian/Makefile
help/Makefile
help/C/Makefile
help/C/figures/Makefile
help/ja/Makefile
intl/Makefile
macros/Makefile
man/Makefile
man/gtranslator.1
po/Makefile.in
src/Makefile
src/backends/Makefile
src/backends/docbook/Makefile
src/backends/text/Makefile
src/pixmaps/Makefile
])

dnl ------------------------------------------------------------------
dnl Print an information screen at the end of the process
dnl ------------------------------------------------------------------
echo "
------------------------------------------------------------------
-- gtranslator compilation options

gtranslator version	  :	${VERSION}

bind po & co. files	  :	${enable_mime_bind:-possibly}	

CFLAGS                 	  :	${CFLAGS}

debug friendly build	  :	${enable_debug:-no}

Scrollkeeper integration  :	${sc_present:-no}
------------------------------------------------------------------
-- For suggestions, problems & bug reports for gtranslator please
    use http://bugzilla.gnome.org/enter_bug.cgi?product=gtranslator

*** IMPORTANT *** 

This is a development version of gtranslator.  You should be using a stable
version, which is available in the 'gtranslator-gnome-1' branch in CVS.
"
