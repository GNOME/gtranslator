dnl -------------------------------------------------------------------
dnl Process this file with autoconf to produce a configure script .
dnl -------------------------------------------------------------------

AC_INIT(src/parse.c)
AM_CONFIG_HEADER(config.h)

dnl -------------------------------------------------------------------
dnl Versioning stuff
dnl -------------------------------------------------------------------
MAINVERSION=0
SUBVERSION=31
VERSION=$MAINVERSION.$SUBVERSION
AC_SUBST(SUBVERSION)
AC_SUBST(MAINVERSION)

dnl -------------------------------------------------------------------
dnl Init automake and the maintainer mode macros.
dnl -------------------------------------------------------------------
AM_INIT_AUTOMAKE(gtranslator, $VERSION)
AM_MAINTAINER_MODE

dnl -------------------------------------------------------------------
dnl Pick up the Gnome macros .
dnl -------------------------------------------------------------------
AM_ACLOCAL_INCLUDE(macros)

dnl -------------------------------------------------------------------
dnl The standard checks for the compiler and the X includes/libs.
dnl -------------------------------------------------------------------

GNOME_INIT
AC_ISC_POSIX
AC_PROG_CC
AC_HEADER_STDC
GNOME_X_CHECKS

dnl -------------------------------------------------------------------
dnl Libtool for the libgtranslator directory and the directory.
dnl -------------------------------------------------------------------
AM_PROG_LIBTOOL

dnl -------------------------------------------------------------------
dnl The standard extended GNOME compile warning options ...
dnl -------------------------------------------------------------------
GNOME_COMPILE_WARNINGS

dnl -------------------------------------------------------------------
dnl Check for glib and gtk+ indepententally from the GNOME checks 
dnl -------------------------------------------------------------------
AM_PATH_GLIB(1.2.3)

dnl -------------------------------------------------------------------
dnl We need Gtk+ >= 1.2.4 for D'n'D, do we? I don't know it, but I've 
dnl  got heard somewhere that proper D'n'D was integrated with 1.2.4.
dnl -------------------------------------------------------------------
AM_PATH_GTK(1.2.4)

dnl -------------------------------------------------------------------
dnl Our languages caravane has got settled here in place.( add your's )
dnl -------------------------------------------------------------------
ALL_LINGUAS="da de en_GB es fi fr gl hu ja ko lt nl no pt_BR ru sv tr uk"
AM_GNOME_GETTEXT

dnl -------------------------------------------------------------------
dnl Check for the window-icon feature of gnome-libs > 1.0.59.
dnl -------------------------------------------------------------------
MY_CFLAGS=$CFLAGS
MY_LIBS=$LIBS
CFLAGS="$CFLAGS `gnome-config --cflags gnomeui`"
LIBS="$LIBS `gnome-config --libs gnomeui`"
AC_CHECK_LIB(gnomeui, gnome_window_icon_set_default_from_file, 
	AC_MSG_RESULT(["·:0 Will use window-icon feature..."])
	AC_DEFINE(USE_WINDOW_ICON)
	,[
	w
	AC_MSG_RESULT(["·:0 Cannot use the window-icon feature as you have not got a recent enough gnome-libs version"])])
CFLAGS=$MY_CFLAGS
LIBS=$MY_LIBS

dnl -------------------------------------------------------------------
dnl Get the idl parameters.
dnl -------------------------------------------------------------------
IDL_CFLAGS=`gnome-config --cflags idl`
IDL_LIBS=`gnome-config --libs idl`
AC_SUBST(IDL_CFLAGS)
AC_SUBST(IDL_LIBS)

dnl -------------------------------------------------------------------
dnl Check for ORBit >= 0.5.1, OAF 0.3
dnl -------------------------------------------------------------------
AM_PATH_ORBIT(0.5.1)
AM_PATH_OAF(0.3.0)

dnl -------------------------------------------------------------------
dnl Check for GConf >= 0.9 and the stuff around it ..
dnl -------------------------------------------------------------------
gconf_is_present=yes
AM_PATH_GCONF(0.9.0,,gconf_is_present=no,gconf-gtk)
if test "q$gconf_is_present" = "qno" ; then
	AC_MSG_RESULT(["·:0 Will NOT use GConf..."])
else
	if test "ENV$NO_GCONF" = "ENVyes" ; then
	gconf_is_present=no
	AC_MSG_RESULT(["·:0 Will not use the found GConf as \$NO_GCONF is set to _yes_ ..."])
	else
	AC_MSG_RESULT(["·:0 Will use GConf..."])
	AC_DEFINE(GCONF_IS_PRESENT)
	fi
fi
GNOME_CFLAGS=`gnome-config --cflags gnome`
GNOME_LIBS=`gnome-config --libs gnome`
GNOME_LIB_CONF_DIR=`gnome-config --libdir`
AC_SUBST(GNOME_CFLAGS)
AC_SUBST(GNOME_LIBS)
AC_SUBST(GNOME_LIB_CONF_DIR)
AM_CONDITIONAL(GCONF_IS_PRESENT, test x$gconf_is_present = xyes)

dnl -------------------------------------------------------------------
dnl Check for libxml >= 1.8.9 and get the cflags and libs.
dnl -------------------------------------------------------------------
AC_PATH_PROG(XML_CONFIG, xml-config, nej)
if test "our$XML_CONFIG" = "ournej" ; then
	AC_MSG_ERROR([No xml-config found ! Please install libxml first.])
else
	AC_MSG_CHECKING([for libxml >= 1.8.9])
	XML_VERSION=`xml-config --version|sed -e 's/\.//g'`
	if test "$XML_VERSION" -lt 189 ; then
		AC_MSG_RESULT("no")
		AC_MSG_ERROR("Your libxml is too old!")
	else
		AC_MSG_RESULT(["yes, version `xml-config --version`"])
	fi
	XML_CFLAGS=`xml-config --cflags`
	XML_LIBS=`xml-config --libs`
	AC_SUBST(XML_CFLAGS)
	AC_SUBST(XML_LIBS)
fi

dnl -------------------------------------------------------------------
dnl Check for GnomeVFS via this crappy check and get the libs and 
dnl  compile flags for it or disable GTR_VFS_HANDLE.
dnl -------------------------------------------------------------------
vfs_stuff=no
AC_MSG_CHECKING(for GnomeVFS >= 0.4.1)
if gnome-config --cflags vfs 2>&1 >/dev/null ; then
	dnl -----------------------------------------------------------
	dnl Temporarily we're now happy :-)
	dnl -----------------------------------------------------------
	vfs_stuff=yes
	dnl -----------------------------------------------------------
	dnl Test the version of GnomeVFS to be >= 0.4.1
	dnl -----------------------------------------------------------
	VFS_VERSION=`gnome-config --modversion vfs|sed -e 's/.*-//g' -e 's/\.//g'`
	if test "$VFS_VERSION" -ge 0410 ; then
		AC_MSG_RESULT([" "
"·:0 Right your GnomeVFS is recent enough for us... "])	
	else
		vfs_stuff=no
		AC_MSG_RESULT([" "
"·:0 Your GnomeVFS is too old, it is $VFS_VERSION < gnome-vfs 0.4"])
	fi
	dnl -----------------------------------------------------------
	dnl Get the CFLAGS and the LIBS
	dnl -----------------------------------------------------------
	VFS_CFLAGS=`gnome-config --cflags vfs`
	VFS_LIBS=`gnome-config --libs vfs`
	dnl -----------------------------------------------------------
	dnl Define the USE_VFS_STUFF directive...
	dnl -----------------------------------------------------------
	if test "x$vfs_stuff" = "xyes" ; then
		AC_DEFINE(USE_VFS_STUFF)
		AC_SUBST(VFS_CFLAGS)
		AC_SUBST(VFS_LIBS)
	fi	
else
	dnl -----------------------------------------------------------
	dnl Oh, no GnomeVFS ;-(
	dnl -----------------------------------------------------------
	AC_MSG_RESULT([" "
"·:0 GnomeVFS was not found... Not building, linking with VFS stuff."])	
fi
AM_CONDITIONAL(USE_VFS_STUFF, test x$vfs_stuff = xyes)

dnl -------------------------------------------------------------------
dnl Substitute PACKAGE_LOCALE_DIR in config.h.
dnl -------------------------------------------------------------------
if test "x${prefix}" = "xNONE"; then
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${ac_default_prefix}/${DATADIRNAME}/locale")
else
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${prefix}/${DATADIRNAME}/locale")
fi

dnl -------------------------------------------------------------------
dnl Simply substitute the icon-location directory
dnl in gtranslator.keys.in .
dnl -------------------------------------------------------------------
PIXMAP_DIR="`gnome-config --datadir`/pixmaps/mc"
AC_SUBST(PIXMAP_DIR)

dnl -------------------------------------------------------------------
dnl Whether to bind the po-files with gtranslator/bind the mime-type ?
dnl -------------------------------------------------------------------
AC_ARG_ENABLE(mime-bind,
	[  --enable-mime-bind=[yes/no]      Bind the po-files mime-type to gtranslator],,enable_mime_bind=yes)
AC_MSG_CHECKING([if to bind the po-files with gtranslator ])
if test "x$enable_mime_bind" = "xyes" ; then
	AC_DEFINE(BIND_MIME)
	AC_MSG_RESULT([" "
"·:0 Will install the mime and keys files."])
else
	AC_MSG_RESULT([" "
"·:0 Not installing the mime and keys files for the mime-types."])
fi
AM_CONDITIONAL(BIND_MIME,test x$enable_mime_bind = xyes)


dnl -------------------------------------------------------------------
dnl Get the message-db.d directories place.
dnl -------------------------------------------------------------------
AC_ARG_WITH(message-db-dir,
     [  --with-message-db-dir=DIR        Define the message-db.d location],,with_message_db_dir=NONE)
AC_MSG_CHECKING([where to put the message-db.d directory ])
if test "z$with_message_db_dir" = "zNONE" ; then
	AC_MSG_RESULT([" "
"·:0 Will put the message-db.d dir into gtranslators datadir."])
	MESSAGE_DB_DIR=${datadir}/gtranslator
	AC_SUBST(MESSAGE_DB_DIR)
else
	AC_MSG_RESULT([" "
"·:0 Will put the message-db.d dir to ${with_message_db_dir}."])
	MESSAGE_DB_DIR=${with_message_db_dir}
	AC_SUBST(MESSAGE_DB_DIR)
fi

dnl -------------------------------------------------------------------
dnl Enable debug builds.
dnl -------------------------------------------------------------------
AC_ARG_WITH(debug-specs,
     [  --with-debug-specs		   Build everything with debug informations],,with_debug_specs=no)
AC_MSG_CHECKING([for a debuggy build ])
if test "rt$with_debug_specs" = "rtno" ; then
	AC_MSG_RESULT([" "
"·:0 No, building everything without any debugging informations and flags..."])
	CFLAGS="`echo $CFLAGS|sed -e 's/-g.//g'`"
else
	AC_MSG_RESULT([" "
"·:0 Yes, building everything WITH debugging informations and flags..."])
	CFLAGS="`echo $CFLAGS|sed -e 's/-O.//g'` -ggdb"
fi

dnl -------------------------------------------------------------------
dnl For the extremists of the 'gcc-&-Werror-are-friends' department.
dnl -------------------------------------------------------------------
AC_ARG_WITH(extreme-gcc-flags,
     [  --with-extreme-gcc-flags	   Set the CFLAGS to the most extreme values],extreme_gcc_flags=yes,)
AC_MSG_CHECKING([for extremists-ala-gcc-Werror ])
if test "gcc$extreme_gcc_flags" = "gccyes" ; then
	AC_MSG_RESULT([" "
"·:0 Yes, you are getting your -Werror -Wformat etc. ..."])
	CFLAGS="$CFLAGS -Werror -Wtrigraphs -Wformat -Wchar-subscripts"
	CFLAGS="$CFLAGS	-Wimplicit -Wreturn-type -Wswitch -Wcomment"
else
	AC_MSG_RESULT([" "
"·:0 Huh, no gcc extremist =?"])
fi

dnl -------------------------------------------------------------------
dnl Try to get a pkg-config directory for us ..
dnl -------------------------------------------------------------------
AC_MSG_CHECKING([where the pkg-config file should go in])
PKG_CONF_CONFFILE_DIR="$PKG_CONFIG_PATH"
if test "foo${PKG_CONF_CONFFILE_DIR}" = "foo" ; then
	dnl ----------------------------------------------------------
	dnl Try the standard locations if no variable $PKG_CONFIG_PATH
	dnl  has been defined on shell-level.
	dnl ----------------------------------------------------------
	for dirX in /usr/lib/pkgconfig /usr/libexec/pkgconfig /usr/local/lib/pkgconfig /usr/local/libexec/pkgconfig
		do
			if test -d "$dirX" ; then
				PKG_CONF_CONFFILE_DIR="$dirX"
				AC_MSG_RESULT([" "
"·:0 Taking ${PKG_CONF_CONFFILE_DIR} as pkg-config supplement files directory"])
			fi
		done
	dnl ----------------------------------------------------------
	dnl Test if we did find any pkgconfig directory and use the 
	dnl  default pkgconfig dir "/usr/lib/pkgconfig" in this case..
	dnl ----------------------------------------------------------
	if test "foo${PKG_CONF_CONFFILE_DIR}" = "foo" ; then
		AC_MSG_RESULT([" "
"·:0 No existing pkgconfig directory found, using /usr/lib/pkgconfig ..."])
		PKG_CONF_CONFFILE_DIR="/usr/lib/pkgconfig"
	fi
else
	AC_MSG_RESULT([" "
"·:0 Taking ${PKG_CONF_CONFFILE_DIR} as pkg-config supplement files directory"])
fi
AC_SUBST(PKG_CONF_CONFFILE_DIR)

dnl -------------------------------------------------------------------
dnl Get the date for the man-page and substitute it there and anywhere.
dnl -------------------------------------------------------------------
MY_DATE=`date +%Y-%m-%d`
AC_SUBST(MY_DATE)

dnl -------------------------------------------------------------------
dnl Put out the configured files.
dnl -------------------------------------------------------------------
AC_OUTPUT([
Makefile
data/Makefile
data/desktop/Makefile
data/gconf/Makefile
data/gnome-config/gtranslatorConf.sh
data/gnome-config/Makefile
data/mime/Makefile
data/mime/gtranslator.keys
data/oaf/Makefile
data/pkg-config/Makefile
data/pkg-config/gtranslator.pc
data/scripts/Makefile
data/ui/Makefile
macros/Makefile
intl/Makefile
src/Makefile
po/Makefile.in
help/Makefile
help/C/Makefile
help/C/index.html
help/tr/Makefile
help/tr/index.html
man/Makefile
man/gtranslator.1
libgtranslator/Makefile
message-db.d/Makefile
])

dnl ------------------------------------------------------------------
dnl Print an information screen at the end of the process
dnl ------------------------------------------------------------------
if test "q${with_message_db_dir}" = "qNONE" ; then
	with_message_db_dir=
fi
grep -sq USE_WINDOW_ICON\ 1  config.h && \
use_window_icon_feature=yes
echo "
------------------------------------------------------------------
-- gtranslator compilage options

gtranslator version	:	${VERSION}

bind the po-files  
 with gtranslator  	:	${enable_mime_bind:-possibly}	

where to put the
 message-db.d dir  	:	${with_message_db_dir:-gtranslator datadir}

use the window-icon
 feature of gnome-libs  :	${use_window_icon_feature:-no}

if we can use GConf
 for the config-stuff	:	${gconf_is_present:-no}

if libgtranslator can
 use GnomeVFS stuff	:	${vfs_stuff:-no}

if everything should be 
 compiled debuggy	:	${with_debug_specs:-no}

if you're getting your
 loved -Werror etc.	:	${extreme_gcc_flags:-no}

------------------------------------------------------------------
"
