dnl -------------------------------------------------------------------
dnl Process this file with autoconf to produce a configure script.
dnl -------------------------------------------------------------------
dnl (C) 2000-2002 Fatih Demir <kabalak@gtranslator.org>
dnl -------------------------------------------------------------------
AC_INIT(src/main.c)
AM_INIT_AUTOMAKE(gtranslator, 0.42)
AM_CONFIG_HEADER(config.h)
AM_MAINTAINER_MODE

dnl -------------------------------------------------------------------
dnl Use intltools.
dnl -------------------------------------------------------------------
AC_PROG_INTLTOOL(0.11)

dnl -------------------------------------------------------------------
dnl Libtool -- needed for backends.
dnl -------------------------------------------------------------------
AC_ISC_POSIX
AC_DISABLE_STATIC
AM_PROG_LIBTOOL

dnl -------------------------------------------------------------------
dnl Pick up the Gnome macros .
dnl -------------------------------------------------------------------
AM_ACLOCAL_INCLUDE(macros)

dnl -------------------------------------------------------------------
dnl The standard checks for the compiler and the X includes/libs.
dnl -------------------------------------------------------------------

AC_HEADER_STDC
GNOME_INIT
GNOME_X_CHECKS

dnl -------------------------------------------------------------------
dnl The standard extended GNOME compile warning options ...
dnl -------------------------------------------------------------------
GNOME_COMPILE_WARNINGS

dnl -------------------------------------------------------------------
dnl Our languages caravane has got settled here in place (add your's).
dnl -------------------------------------------------------------------
ALL_LINGUAS="az ca da de el en_GB es et eu fi fr gl hu it ja ko lt nl nn no pl pt pt_BR ru sk sv ta tr uk vi wa zh_CN zh_TW"
AM_GNOME_GETTEXT

dnl -----------------------------------------------------------
dnl Test the version of GnomeVFS to be >= 0.4.1
dnl -----------------------------------------------------------
AC_MSG_CHECKING(for GnomeVFS >= 0.4.1)
if gnome-config --libs vfs 2>&1 >/dev/null ; then
	VFS_VERSION=`gnome-config --modversion vfs | sed -e 's/^[[^0-9]]*//' | \
	awk -F. '{ print $1 * 10000 + $2 * 100 + $3;}'`
	
	if test "$VFS_VERSION" -ge 401 ; then
		AC_MSG_RESULT(found)			
	else
		AC_MSG_ERROR([
------------------------------------------------------------------------------
Please install GnomeVFS >= 0.4.1 and try again.
-----------------------------------------------------------------------------])
	fi
else
	AC_MSG_ERROR([
-----------------------------------------------------------------------------	
Please install GnomeVFS before trying to compile gtranslator.
-----------------------------------------------------------------------------])
fi

dnl -------------------------------------------------------------------
dnl Define the compile flags for gtranslator
dnl -------------------------------------------------------------------
VFS_CFLAGS=`gnome-config --cflags vfs`
VFS_LIBS=`gnome-config --libs vfs`
AC_SUBST(VFS_CFLAGS)
AC_SUBST(VFS_LIBS)

dnl -------------------------------------------------------------------
dnl Check for gal >= 0.11.99.
dnl -------------------------------------------------------------------
AC_MSG_CHECKING(for gal >= 0.11.99)
if gnome-config --libs gal 2>&1 >/dev/null ; then
	dnl -----------------------------------------------------------
	dnl gal is present; check for the right version numbers.
	dnl -----------------------------------------------------------
	GAL_VERSION=`gnome-config --modversion gal | sed -e 's/^[[^0-9]]*//' | \
	awk -F. '{ print $1 * 10000 + $2 * 100 + $3;}'`
	
	if test "$GAL_VERSION" -ge 1199 ; then
		AC_MSG_RESULT(found)			
	else
		dnl ---------------------------------------------------
		dnl Tell the user that we've found gal, but it's too
		dnl  old for us.
		dnl ---------------------------------------------------
		AC_MSG_ERROR([
-----------------------------------------------------------------------------
Please install gal >= 0.11.99 and try configuring again.
-----------------------------------------------------------------------------])
	fi
else
	AC_MSG_ERROR([
-----------------------------------------------------------------------------
Please install gal before configuring again.
-----------------------------------------------------------------------------])
fi

GAL_CFLAGS=`gnome-config --cflags gal`
GAL_LIBS=`gnome-config --libs gal`
AC_SUBST(GAL_CFLAGS)
AC_SUBST(GAL_LIBS)

dnl -------------------------------------------------------------------
dnl Check for GConf >= 1.0 and the stuff around it..
dnl -------------------------------------------------------------------
gconf_is_present=yes
if $try_gconf; then
	AM_PATH_GCONF(1.0,,gconf_is_present=no,gconf-gtk)
	if test "x$gconf_is_present" = "xno" ; then
		AC_MSG_ERROR([
-----------------------------------------------------------------------------
No GConf >= 1.0 found! GConf is now mandatory for gtranslator...
-----------------------------------------------------------------------------])
	fi
fi

dnl -------------------------------------------------------------------
dnl Check for the window-icon feature of gnome-libs >= 1.0.60.
dnl -------------------------------------------------------------------
MY_CFLAGS=$CFLAGS
MY_LIBS=$LIBS
CFLAGS="$CFLAGS `gnome-config --cflags gnomeui`"
LIBS="$LIBS `gnome-config --libs gnomeui`"
use_window_icon=
AC_CHECK_LIB(gnomeui, gnome_window_icon_set_default_from_file, use_window_icon=yes)
CFLAGS=$MY_CFLAGS
LIBS=$MY_LIBS

if test "%${use_window_icon}" != "%yes" ; then
	AC_MSG_ERROR([
-----------------------------------------------------------------------------
You need at least gnome-libs 1.0.60 for compiling gtranslator. Please install it.
-----------------------------------------------------------------------------])
fi	

dnl -------------------------------------------------------------------
dnl Simply substitute the icon-location directory in the keys file.
dnl -------------------------------------------------------------------
PIXMAP_DIR="`gnome-config --datadir`/pixmaps"
AC_SUBST(PIXMAP_DIR)

dnl -------------------------------------------------------------------
dnl Whether to bind the po-files with gtranslator/bind the mime-type ?
dnl -------------------------------------------------------------------
AC_ARG_ENABLE(mime-bind,
	[  --enable-mime-bind=[yes/no]   Bind the po-files mime-type to gtranslator],, enable_mime_bind=yes)
AC_MSG_CHECKING([if to bind the po-files with gtranslator ])
if test "x$enable_mime_bind" = "xyes" ; then
	AC_DEFINE(BIND_MIME)
fi
AC_MSG_RESULT($enable_mime_bind)
AM_CONDITIONAL(BIND_MIME,test x$enable_mime_bind = xyes)

dnl -------------------------------------------------------------------
dnl Check for Scrollkeeper and set the corresponding conditional var if
dnl  it could be found.
dnl -------------------------------------------------------------------
AC_CHECK_PROG(sc_present, scrollkeeper-config, yes, no)
if test "z$sc_present" = "zyes" ; then
	AC_MSG_CHECKING([for scrollkeeper >= 0.1.4])
	_VERSION=`scrollkeeper-config --version|sed -e 's/\.//g'`
	if test $_VERSION -ge 014 -o $_VERSION -ge 02; then
		AC_MSG_RESULT(yes)
		SC_STATEDIR=`scrollkeeper-config --pkglocalstatedir`
		SC_OMFDIR=`scrollkeeper-config --omfdir`
	else
		AC_MSG_RESULT(no)
		sc_present=no
	fi
fi
AC_SUBST(SC_OMFDIR)
AC_SUBST(SC_STATEDIR)
AM_CONDITIONAL(SCROLLKEEPER_PRESENT, test z$sc_present = zyes)

dnl -------------------------------------------------------------------
dnl Check for jw to get the docs building fine with RH 7.1
dnl -------------------------------------------------------------------
AC_CHECK_PROG(jw_present, jw, yes, no)
AM_CONDITIONAL(JW_PRESENT, test e$jw_present = eyes)

dnl -------------------------------------------------------------------
dnl Enable debug builds.
dnl -------------------------------------------------------------------
AC_ARG_ENABLE(debug,
	[  --enable-debug                Turn on debugging flags [default=yes]],
	,enable_debug=yes)
AC_MSG_CHECKING([if a debug friendly version should be build])
if test "x$enable_debug" = "xyes" ; then
	CFLAGS="$CFLAGS -ggdb -Werror"
else
	CFLAGS="`echo $CFLAGS|sed -e s/-g.//g -e s/-ggdb//g`"
fi
AC_MSG_RESULT($enable_debug)

dnl -------------------------------------------------------------------
dnl Get the date for the man-page and substitute it there and anywhere.
dnl -------------------------------------------------------------------
MY_DATE=`date +%Y-%m-%d`
AC_SUBST(MY_DATE)

dnl -------------------------------------------------------------------
dnl The most important check for a library at all!
dnl -------------------------------------------------------------------
AC_CHECK_LIB(ife, do_you_think_we_are_just_geeks,,)

dnl -------------------------------------------------------------------
dnl Put out the configured files.
dnl -------------------------------------------------------------------
AC_OUTPUT([
Makefile
gtranslator.spec
data/Makefile
data/colorschemes/Makefile
data/desktop/Makefile
data/etspecs/Makefile
data/mime/Makefile
data/mime/gtranslator.keys
data/pixmaps/Makefile
data/scripts/Makefile
debian/Makefile
help/Makefile
help/C/Makefile
help/C/figures/Makefile
help/ja/Makefile
intl/Makefile
macros/Makefile
man/Makefile
man/gtranslator.1
po/Makefile.in
src/Makefile
src/backends/Makefile
src/backends/docbook/Makefile
src/backends/text/Makefile
src/pixmaps/Makefile
])

dnl ------------------------------------------------------------------
dnl Print an information screen at the end of the process
dnl ------------------------------------------------------------------
echo "
------------------------------------------------------------------
-- gtranslator compilation options

gtranslator version	  :	${VERSION}

bind po & co. files	  :	${enable_mime_bind:-possibly}	

GAL & GnomeVFS version	  :	`gnome-config --modversion gal|sed -e 's/.*-//g'` & `gnome-config --modversion vfs|sed -e 's/.*-//g'`

CFLAGS                 	  :	${CFLAGS}

debug friendly build	  :	${enable_debug:-no}

Scrollkeeper integration  :	${sc_present:-no}
------------------------------------------------------------------
-- For suggestions, problems & bug reports for gtranslator please
    use http://bugzilla.gnome.org/enter_bug.cgi?product=gtranslator
"
