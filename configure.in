dnl -------------------------------------------------------------------
dnl Process this file with autoconf to produce a configure script .
dnl -------------------------------------------------------------------

AC_INIT(README)
AM_INIT_AUTOMAKE(gtranslator, 0.20)
AM_CONFIG_HEADER(config.h)
AM_MAINTAINER_MODE

dnl -------------------------------------------------------------------
dnl Pick up the Gnome macros .
dnl -------------------------------------------------------------------
AM_ACLOCAL_INCLUDE(macros)

dnl -------------------------------------------------------------------
dnl The standard ./configure checks and a check for the usleep()-func.
dnl -------------------------------------------------------------------

GNOME_INIT
AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC
AC_CHECK_FUNCS(usleep)
GNOME_X_CHECKS

dnl -------------------------------------------------------------------
dnl Check for the window-icon feature of gnome-libs > 1.0.59.
dnl -------------------------------------------------------------------
MY_CFLAGS=$CFLAGS
MY_LIBS=$LIBS
CFLAGS="$CFLAGS `gnome-config --cflags gnomeui`"
LIBS="$LIBS `gnome-config --libs gnomeui`"
AC_CHECK_LIB(gnomeui, gnome_window_icon_set_default_from_file, 
	AC_MSG_RESULT(["\*\*\*\*\*\*\*\*\*\* Will use window-icon feature"])
	AC_DEFINE(USE_WINDOW_ICON)
	,[
	AC_MSG_RESULT(["\*\*\*\*\*\*\*\*\*\* Cannot use the window-icon feature as you have not got a recent enough gnome-libs version"])])
CFLAGS=$MY_CFLAGS
LIBS=$MY_LIBS

dnl -------------------------------------------------------------------
dnl Check for glib and gtk+ indepententally from the GNOME checks 
dnl -------------------------------------------------------------------
AM_PATH_GLIB(1.2.3)

dnl -------------------------------------------------------------------
dnl We need Gtk+ >= 1.2.4 for D'n'D, do we? I don't know it, but I've 
dnl  got heard somewhere that proper D'n'D was integrated with 1.2.4.
dnl -------------------------------------------------------------------
AM_PATH_GTK(1.2.4)

dnl -------------------------------------------------------------------
dnl Our languages caravane has got settled here in place.( add your's )
dnl -------------------------------------------------------------------
ALL_LINGUAS="da de hu ja ko nl no ru sv tr uk"
AM_GNOME_GETTEXT

dnl -------------------------------------------------------------------
dnl Get the idl parameters.
dnl -------------------------------------------------------------------
IDL_CFLAGS=`gnome-config --cflags idl`
IDL_LIBS=`gnome-config --libs idl`
AC_SUBST(IDL_CFLAGS)
AC_SUBST(IDL_LIBS)

dnl -------------------------------------------------------------------
dnl Check for ORBit >= 0.5.1 and OAF 0.3
dnl -------------------------------------------------------------------
AM_PATH_ORBIT(0.5.1)
AM_PATH_OAF(0.3.0)

dnl -------------------------------------------------------------------
dnl Substitute PACKAGE_LOCALE_DIR in config.h.
dnl -------------------------------------------------------------------
if test "x${prefix}" = "xNONE"; then
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${ac_default_prefix}/${DATADIRNAME}/locale")
else
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${prefix}/${DATADIRNAME}/locale")
fi

dnl -------------------------------------------------------------------
dnl Simply substitute the icon-location directory
dnl in gtranslator.keys.in .
dnl -------------------------------------------------------------------
PIXMAP_DIR="`gnome-config --datadir`/pixmaps/mc"
AC_SUBST(PIXMAP_DIR)

dnl -------------------------------------------------------------------
dnl Whether to bind the po-files with gtranslator/bind the mime-type ?
dnl -------------------------------------------------------------------
AC_ARG_ENABLE(mime-bind,
	[  --enable-mime-bind=[no/yes]      Bind the po-files mime-type to gtranslator],,enable_mime_bind=no)
AC_MSG_CHECKING([if to bind the po-files with gtranslator ])
if test "x$enable_mime_bind" = "xyes" ; then
	AC_DEFINE(BIND_MIME)
	AC_MSG_RESULT(["Ok,"
"********** Will install the mime and keys files."])
else
	AC_MSG_RESULT(["Ok, "
"********** Not installing the mime and keys files for the mime-types."])
fi
AM_CONDITIONAL(BIND_MIME,test x$enable_mime_bind = xyes)

dnl -------------------------------------------------------------------
dnl The standard extended GNOME compile warning options ...
dnl -------------------------------------------------------------------
GNOME_COMPILE_WARNINGS

dnl -------------------------------------------------------------------
dnl Get the date for the man-page and substitute it there and anywhere.
dnl -------------------------------------------------------------------
MY_DATE=`date +%Y-%m-%d`
AC_SUBST(MY_DATE)

dnl -------------------------------------------------------------------
dnl Put out the configured files.
dnl -------------------------------------------------------------------
AC_OUTPUT([
Makefile
data/Makefile
data/gtranslator.keys
macros/Makefile
help/Makefile
help/C/Makefile
help/tr/Makefile
man/Makefile
man/gtranslator.1
libgtranslator/Makefile
src/Makefile
intl/Makefile
po/Makefile.in
])

dnl ------------------------------------------------------------------
dnl Print an information screen at the end of the process
dnl ------------------------------------------------------------------
grep -sq USE_WINDOW_ICON\ 1  config.h && \
use_window_icon_feature=yes
echo "
------------------------------------------------------------------
-- gtranslator compilage options

gtranslator version	:	${VERSION}

bind the po-files  
 with gtranslator  	:	${enable_mime_bind:-possibly}	

(X)Emacs-friendly
 about message     	:	${enable_emacs_lover:-possibly}

use the window-icon
 feature of gnome-libs  :	${use_window_icon_feature:-no}
------------------------------------------------------------------
"
